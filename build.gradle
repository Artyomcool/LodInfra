plugins {
    id 'java'
    id 'application'
    id 'com.palantir.graal' version '0.10.0'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

group 'com.github.artyomcool'
version '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    modularity.inferModulePath = true
}

compileJava {
    options.compilerArgs.addAll([
            '--add-exports',
            'javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED',

            '--add-exports',
            'javafx.graphics/com.sun.javafx.application=ALL-UNNAMED',

            '--add-exports',
            'javafx.graphics/com.sun.javafx.scene.traversal=ALL-UNNAMED',

            '--add-exports',
            'javafx.graphics/com.sun.glass.utils=ALL-UNNAMED',

            '--add-exports',
            'javafx.graphics/com.sun.glass.ui=ALL-UNNAMED',

            '--add-exports',
            'javafx.controls/com.sun.javafx.scene.control.behavior=ALL-UNNAMED',

            '--add-exports',
            'javafx.controls/com.sun.javafx.scene.control.inputmap=ALL-UNNAMED',

            '--add-exports',
            'org.controlsfx.controls/impl.org.controlsfx.skin=ALL-UNNAMED'
    ])
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.apache.poi:poi:4.1.2') {
        exclude group: 'org.apache.logging.log4j'
    }
    implementation('org.apache.poi:poi-ooxml:4.1.2') {
        exclude group: 'org.apache.logging.log4j'
    }
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'ar.com.hjg:pngj:2.1.0'

    implementation 'org.graalvm.nativeimage:svm:22.0.0.2'

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.controlsfx:controlsfx:11.1.1'
    implementation 'com.jfoenix:jfoenix:9.0.10'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

javafx {
    version = '11'
    modules = ['javafx.graphics','javafx.controls']
}

application {
    mainClass = 'com.github.artyomcool.lodinfra.Pack'
    applicationDefaultJvmArgs = [
            '--add-opens',
            'javafx.base/com.sun.javafx.runtime=ALL-UNNAMED',
            '--add-opens',
            'javafx.controls/com.sun.javafx.scene.control.behavior=ALL-UNNAMED',
            '--add-opens',
            'javafx.controls/com.sun.javafx.scene.control=ALL-UNNAMED',
            '--add-opens',
            'javafx.base/com.sun.javafx.binding=ALL-UNNAMED',
            '--add-opens',
            'javafx.base/com.sun.javafx.event=ALL-UNNAMED',
            '--add-opens',
            'javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED'
    ]
}

graal {
    graalVersion '22.0.0.2'
    outputName 'pack'

    mainClass 'com.github.artyomcool.lodinfra.Pack'

    javaVersion '11'
    option '--no-fallback'
    option '--allow-incomplete-classpath'
    option '-H:+AddAllCharsets'
    option '--features=com.github.artyomcool.lodinfra.NativeImage'
    option '-H:IncludeResources=(.*\\.css)'
    option '-H:+ReportExceptionStackTraces'
    option '--verbose'
    option '-J-Xmx16G'
}

test {
    useJUnitPlatform()
}