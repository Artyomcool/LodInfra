plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id "org.beryx.jlink" version "2.24.1"
}

group 'com.github.artyomcool'
version '1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    modularity.inferModulePath = true
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll([
            '-g',

            '--add-exports',
            'javafx.graphics/com.sun.javafx.stage=LodInfra',

            '--add-exports',
            'javafx.graphics/com.sun.javafx.application=LodInfra',

            '--add-exports',
            'javafx.graphics/com.sun.javafx.scene.traversal=LodInfra',

            '--add-exports',
            'javafx.graphics/com.sun.glass.utils=LodInfra',

            '--add-exports',
            'javafx.graphics/com.sun.glass.ui=LodInfra',

            '--add-exports',
            'javafx.controls/com.sun.javafx.scene.control.behavior=LodInfra',

            '--add-exports',
            'javafx.controls/com.sun.javafx.scene.control.inputmap=LodInfra',

            '--add-exports',
            'javafx.controls/com.sun.javafx.scene.control=LodInfra',

            '--add-exports',
            'org.controlsfx.controls/impl.org.controlsfx.skin=LodInfra',
    ])
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.apache.poi:poi:4.1.2') {
        exclude group: 'org.apache.logging.log4j'
    }
    implementation('org.apache.poi:poi-ooxml:4.1.2') {
        exclude group: 'org.apache.logging.log4j'
    }
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'ar.com.hjg:pngj:2.1.0'

    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'org.controlsfx:controlsfx:11.1.1'
    implementation 'com.jfoenix:jfoenix:9.0.10'
}

javafx {
    version = '17'
    modules = ['javafx.graphics','javafx.controls']
}

def opens = [
        '--add-opens',
        'javafx.base/com.sun.javafx.runtime=LodInfra',
        '--add-opens',
        'javafx.base/com.sun.javafx.runtime=com.jfoenix',
        '--add-opens',
        'javafx.controls/com.sun.javafx.scene.control.behavior=LodInfra',
        '--add-opens',
        'javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix',
        '--add-opens',
        'javafx.controls/com.sun.javafx.scene.control=LodInfra',
        '--add-opens',
        'javafx.controls/com.sun.javafx.scene.control=com.jfoenix',
        '--add-opens',
        'javafx.base/com.sun.javafx.binding=LodInfra',
        '--add-opens',
        'javafx.base/com.sun.javafx.binding=com.jfoenix',
        '--add-opens',
        'javafx.base/com.sun.javafx.event=LodInfra',
        '--add-opens',
        'javafx.base/com.sun.javafx.event=com.jfoenix',
        '--add-opens',
        'javafx.graphics/com.sun.javafx.stage=LodInfra',
        '--add-opens',
        'javafx.graphics/com.sun.javafx.stage=com.jfoenix',
        '--add-opens',
        'org.controlsfx.controls/impl.org.controlsfx.skin=LodInfra'
]

application {
    mainClass = 'com.github.artyomcool.lodinfra.Pack'
    mainModule = 'LodInfra'
    applicationDefaultJvmArgs += opens
}

jlink {
    options = ['--compress=2', '--no-header-files', '--no-man-pages', '--include-locales=en,ru']

    launcher {
        name = 'pack'
        noConsole = false
        jvmArgs += opens + '-Xmx2g'
    }

    mergedModule {
        requires 'java.desktop'
        requires 'jdk.charsets'
        requires 'java.logging'
    }


}