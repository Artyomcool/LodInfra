plugins {
    id 'java'
    id 'application'
    id 'com.palantir.graal' version '0.10.0'
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

group 'com.github.artyomcool'
version '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    modularity.inferModulePath = true
}

compileJava {
    options.compilerArgs.addAll([
            '--add-exports',
            'javafx.graphics/com.sun.javafx.stage=LodInfra.main',

            '--add-exports',
            'javafx.graphics/com.sun.javafx.application=LodInfra.main',

            '--add-exports',
            'javafx.graphics/com.sun.javafx.scene.traversal=LodInfra.main',

            '--add-exports',
            'javafx.controls/com.sun.javafx.scene.control.behavior=LodInfra.main',

            '--add-exports',
            'javafx.controls/com.sun.javafx.scene.control.inputmap=LodInfra.main',

            '--add-exports',
            'org.controlsfx.controls/impl.org.controlsfx.skin=LodInfra.main'
    ])
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.apache.poi:poi:4.1.2') {
        exclude group: 'org.apache.logging.log4j'
    }
    implementation('org.apache.poi:poi-ooxml:4.1.2') {
        exclude group: 'org.apache.logging.log4j'
    }
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'ar.com.hjg:pngj:2.1.0'

    implementation 'org.graalvm.sdk:graal-sdk:21.3.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.controlsfx:controlsfx:11.1.1'
    implementation 'com.jfoenix:jfoenix:9.0.10'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

javafx {
    version = '17.0.2'
    modules = ['javafx.graphics','javafx.controls']
}

application {
    mainClass = 'com.github.artyomcool.lodinfra.Pack'
    applicationDefaultJvmArgs = [
            '--add-opens',
            'javafx.base/com.sun.javafx.runtime=LodInfra.main',
            '--add-opens',
            'javafx.controls/com.sun.javafx.scene.control.behavior=LodInfra.main',
            '--add-opens',
            'javafx.controls/com.sun.javafx.scene.control=LodInfra.main',
            '--add-opens',
            'javafx.base/com.sun.javafx.binding=LodInfra.main',
            '--add-opens',
            'javafx.base/com.sun.javafx.event=LodInfra.main',
            '--add-opens',
            'javafx.graphics/com.sun.javafx.stage=LodInfra.main'
    ]
}

graal {
    graalVersion '22.0.0.2'
    outputName 'pack'

    mainClass 'com.github.artyomcool.lodinfra.Pack'

    javaVersion '17'
    option '--no-fallback'
    //option '--allow-incomplete-classpath'
    option '-H:+AddAllCharsets'
    option '--features=com.github.artyomcool.lodinfra.NativeImage'
    option '-H:IncludeResourceBundles=org.apache.xmlbeans.impl.regex.message'
    option '-H:IncludeResources=(org/apache/poi/schemas/.*)|(schemaorg_apache_xmlbeans/.*)'
    option '-H:+ReportExceptionStackTraces'
}

test {
    useJUnitPlatform()
}
